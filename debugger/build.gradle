apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


// バージョン
def appver_major = 0
def appver_minor = 2
def appver_revision = 0
def appver_suffix = ".alpha"
version = "${appver_major}.${appver_minor}.${appver_revision}${appver_suffix}"

// ライブラリ情報
archivesBaseName = "debugger"
group "work.shion.androidpreparation"


android {
    buildToolsVersion build_tools_version
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion compile_sdk_version
    defaultConfig {
        consumerProguardFiles 'consumer-rules.pro'
        minSdkVersion 21
        targetSdkVersion compile_sdk_version
        versionCode 10000 * appver_major + 100 * appver_minor + appver_revision
        versionName version
    }
}

bintray {
    configurations = ['archives']
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    pkg {
        repo = meta_repository
        name = "${project.group}.${archivesBaseName}"
        licenses = ['MIT']
        websiteUrl = meta_github_url
        vcsUrl = "${meta_github_url}.git"
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
    user = project.hasProperty('bintray_user') ? bintray_user : ''
}

dependencies {
    implementation baseLibrary
    implementation baseUiLibrary

    // Google
    kapt "com.google.auto.service:auto-service:1.0-rc6"

    // Hyperion
    def hyperionVersion = "0.9.27"
    implementation "com.willowtreeapps.hyperion:hyperion-attr:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-build-config:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-core:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-crash:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-disk:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-geiger-counter:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-measurement:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-phoenix:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-plugin:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-recorder:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-shared-preferences:${hyperionVersion}"
    implementation "com.willowtreeapps.hyperion:hyperion-timber:${hyperionVersion}"

    // LeakCanary
    api "com.squareup.leakcanary:leakcanary-android:2.1"

    // Stetho
    def stetho_version = "1.5.1"
    implementation "com.facebook.stetho:stetho:${stetho_version}"
}

install {
    repositories.mavenInstaller {
        pom.project {
            artifactId archivesBaseName
            groupId project.group
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            name archivesBaseName
            packaging 'aar'
            scm {
                connection "${meta_github_url}.git"
                url meta_github_url
            }
            url meta_github_url
            version version
        }
    }
}
