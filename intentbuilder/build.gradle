apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


// バージョン
def appver_major = 1
def appver_minor = 0
def appver_revision = 3
version = "${appver_major}.${appver_minor}.${appver_revision}"

// ライブラリ情報
archivesBaseName = "intentbuilder"
group "work.shion.androidpreparation"


android {
    buildToolsVersion build_tools_version
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion compile_sdk_version
    defaultConfig {
        consumerProguardFiles 'consumer-rules.pro'
        minSdkVersion 21
        targetSdkVersion compile_sdk_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 10000 * appver_major + 100 * appver_minor + appver_revision
        versionName version
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

bintray {
    configurations = ['archives']
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    pkg {
        repo = meta_repository
        name = "${project.group}.${archivesBaseName}"
        licenses = ['MIT']
        websiteUrl = meta_github_url
        vcsUrl = "${meta_github_url}.git"
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
    user = project.hasProperty('bintray_user') ? bintray_user : ''
}

dependencies {

    // AndroidX
    implementation "androidx.collection:collection-ktx:${ver_droidx_collection}"
    implementation "androidx.fragment:fragment:${ver_droidx_fragment}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${ver_droidx_espresso}"

    // Kotlin
    implementation platform("org.jetbrains.kotlin:kotlin-bom:${kotlin_version}")
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    // PermissionsDispathcer
    debugImplementation "org.permissionsdispatcher:permissionsdispatcher:${ver_permissions_dispatcher}"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:${ver_permissions_dispatcher}"

    // UI Test
    androidTestImplementation uiTestLibrary
}

install {
    repositories.mavenInstaller {
        pom.project {
            artifactId archivesBaseName
            groupId project.group
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            name archivesBaseName
            packaging 'aar'
            scm {
                connection "${meta_github_url}.git"
                url meta_github_url
            }
            url meta_github_url
            version version
        }
    }
}


task javadoc(type: Javadoc) {
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
